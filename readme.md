# Overview

A light weight set of handy tools for real world functional programming.

Reduce the gap between working on different systems. Such as watch directory changes on a network file system, operate `spawn` on Windows and Linux, handle async IO api with promise, etc.

Rather than help you decide what to do, it is designed to create possibilities. Most times I use it as the core a build system, even this document is generated by nokit itself.

It's one of the core lib of [nobone](https://github.com/ysmood/nobone).

[![NPM version](https://badge.fury.io/js/nokit.svg)](http://badge.fury.io/js/nokit) [![Build Status](https://travis-ci.org/ysmood/nokit.svg)](https://travis-ci.org/ysmood/nokit) [![Build status](https://ci.appveyor.com/api/projects/status/github/ysmood/nokit?svg=true)](https://ci.appveyor.com/project/ysmood/nokit) [![Deps Up to Date](https://david-dm.org/ysmood/nokit.svg?style=flat)](https://david-dm.org/ysmood/nokit)

## Features

- All async functions will return promise.
- All functions are highly lazy designed, minimum boot time.
- Test on Node 0.8 - 0.12 on Mac, Linux and Windows.
- Light weight and self-reference.

# Installation

As a lib dependency, install it locally: `npm i nokit`.

Nokit has provided a cli tool like GNU Make. If you install it globally like this:

`npm -g i nokit commander`

, then have fun with your `nofile`, it can be js, coffee or livescript. For more information goto the `CLI` section.

# Quick Start

## vs Gulp

Here it will automatically lint, compile, compress and cache files by their extensions. You can goto Drives section to see what extensions are supported,
or write your own.

```coffee
kit = require 'nokit'
drives = kit.require 'drives'

kit.warp 'src/**/*.@(coffee|ls)'
    # # To disable cache.
    # .load drives.reader isCache: false
    .load drives.auto 'lint'
    .load drives.auto 'compile', '.coffee': { bare: false }
    .load drives.auto 'compress'
    .load concat 'main.js'
.run 'dist/path'

```

### Write your own drives

Nokit has already provided some handy example drives, you
can check them in the Drives section. It's fairly easy to
write your own.

```coffee
kit = require 'nokit'
coffee = require 'coffee-script'

# A drive for coffee, a simple curried function.
compiler = (opts) -> ->
    # Change extension from '.coffee' to '.js'.
    @dest.ext = '.js'
    @set coffee.compile(@contents, opts)

# A drive to prepend lisence to each file.
# Here "fileInfo.set" is the same with the "@set".
lisencer = (lisence) -> (fileInfo) ->
    @set lisence + '\n' + @contents

# A drive to concat all files. It will override the default writer.
concat = (outputFile) ->
    all = ''

    # Object.assign
    kit._.assign ->
        all += @contents
    , isWriter: true, onEnd: ->
        # This will enable the auto-cache.
        @deps = kit._.pluck @list, 'path'

        @dest = @to + '/' + outputFile
        @set all

        # Call the overrided writer.
        # Call two times and create two output files.
        @super().then =>
            @dest = @dest + '.info'
            @set = '/* info */\n' + all
            @super()

kit.warp 'src/**/*.coffee'
    .load compiler bare: true
    .load lisencer '/* MIT lisence */'
    .load concat 'bundle.js'
.run 'dist'
.then ->
    kit.log 'Build Done'
```

## CLI

If you want nokit support coffee, you should install it like this:

`npm i -g nokit commander coffee-cache coffee-script`

> It's recommended to use coffee-cache, since large build project may
> consume a large portion of your nofile's startup time. Of course, it's
> optional, you may run `npm i -g nokit commander coffee-script` without
> any problem.

Same works with livescript:

`npm i -g nokit commander LiveScript`

> Remarks: for the sake of boot performance, nokit will only load
> `coffee-cache coffee-script/register`
> by default. For livescript or other precompiler, you have to
> set environment varialbe `nokitPreload` to what you want, such as on unix:
> `export nokitPreload='LiveScript coffee-script/register'`. Different module
> names are separated by spaces.

Create a `nofile.coffee` (or `.js`, `.ls`) at your current working directory
or any of its parents directory. The syntax of `nofile` is almost the same as the Cakefile, only the `option`'s first argument is slightly changed.

Assume your file content is:

```coffee
kit = require 'nokit'

module.exports = (task, option) ->

    option '-w, --hello [world]', 'Just a test option', ''

    # Define a default task, and it depends on the "clean" task.
    task 'default', ['clean'], 'This is a comment info', (opts) ->
        kit.log opts.hello

        # Use colors.
        kit.require 'colors'
        kit.log 'print red words'.red

    task 'clean', ->
        kit.remove 'dist'

    # To add alias to a task, just use space to separate names.
    # Here 'build' and 'b' are the same task.
    task 'build b', ->
        kit.require 'drives'
        kit.warp 'src/**/*.js'
        .load kit.drives.auto 'compile'
        .run 'dist'

    task 'sequential', ['clean', 'build'], true, ->
        kit.log 'Run clean and build non-concurrently.'
```

Then you can run it in command line: `no`. Just that simple, without task
name, `no` will try to call the `default` task directly.

You can run `no -h` to display help info.

Call `no build` or `no b` to run the `build` task.

For real world example, just see the [nofile](nofile.coffee?source) that nokit is using.

For more doc for the `option` goto [commander.js](https://github.com/tj/commander.js).

# Changelog

Goto [changelog](doc/changelog.md)

# API

- ## **[extend_nofs](lib/kit.coffee?source#L29)**

    Nokit extends all the functions of [nofs](https://github.com/ysmood/nofs).
    You can use it as same as nofs. For more info, see the doc:

    [Offline Documentation](?gotoDoc=nofs/readme.md)

    - **<u>example</u>**:

        ```coffee
        kit.readFile('test.txt', 'utf8').then (str) ->
        	console.log str

        kit.outputFile 'a.txt', 'test'
        .then -> kit.log 'done'

        kit.writeJSON 'b.json', { a: 10 }
        .then -> kit.log 'done'

        kit.mkdirs 'b.json', { a: 10 }
        .then -> kit.log 'done'
        ```

- ## **[_](lib/kit.coffee?source#L42)**

    The [lodash](https://lodash.com) lib.

    - **<u>type</u>**: { _Object_ }

    - **<u>example</u>**:

        ```coffee
        kit._.map [1, 2, 3]
        ```

- ## **[async](lib/kit.coffee?source#L93)**

    An throttled version of `Promise.all`, it runs all the tasks under
    a concurrent limitation.
    To run tasks sequentially, use `kit.flow`.

    - **<u>param</u>**: `limit` { _Int_ }

        The max task to run at a time. It's optional.
        Default is Infinity.

    - **<u>param</u>**: `list` { _Array | Function_ }

        If the list is an array, it should be a list of functions or promises,
        and each function will return a promise.
        If the list is a function, it should be a iterator that returns
        a promise, when it returns `kit.async.end`, the iteration ends.

    - **<u>param</u>**: `saveResutls` { _Boolean_ }

        Whether to save each promise's result or
        not. Default is true.

    - **<u>param</u>**: `progress` { _Function_ }

        If a task ends, the resolve value will be
        passed to this function.

    - **<u>return</u>**: { _Promise_ }

    - **<u>example</u>**:

        ```coffee
        urls = [
        	'http://a.com'
        	'http://b.com'
        	'http://c.com'
        	'http://d.com'
        ]
        tasks = [
        	-> kit.request url[0]
        	-> kit.request url[1]
        	-> kit.request url[2]
        	-> kit.request url[3]
        ]

        kit.async(tasks).then ->
        	kit.log 'all done!'

        kit.async(2, tasks).then ->
        	kit.log 'max concurrent limit is 2'

        kit.async 3, ->
        	url = urls.pop()
        	if url
        		kit.request url
        	else
        		kit.async.end
        .then ->
        	kit.log 'all done!'
        ```

- ## **[depsCache](lib/kit.coffee?source#L209)**

    A fast file cache helper. It uses hard link to cache files.

    - **<u>param</u>**: `info` { _Object_ }

        Not optional.
        ```coffee
        {
        	# The first item is the key path, others are
        	# its dependencies.
        	deps: Array

        	# The path of the output file.
        	# If it's undefined, depsCache will try to get cache.
        	dests: Array

        	cacheDir: '.nokit'
        }
        ```

    - **<u>return</u>**: { _Promise_ }

        Resolve a info object.
        ```coffee
        {
        	isNewer: Boolean

        	# { path: mtime }
        	deps: Object

        	# { destPath: cachePath }
        	dests: Object

        	cacheError: undefined | Error
        }
        ```

    - **<u>example</u>**:

        ```coffee
        # Set cache
        kit.depsCache {
        	dests: ['index.css']
        	deps: ['index.less', 'b.less', 'c.less']
        }

        # Get cache
        # You don't have to sepecify 'b.less', 'c.less'.
        kit.depsCache { deps: ['index.less'] }
        .then (cache) ->
        	if cache.isNewer
        		kit.log 'cache is newer'.
        		kit.log cache.dests
        ```

- ## **[colors](lib/kit.coffee?source#L284)**

    The [colors](https://github.com/Marak/colors.js) lib
    makes it easier to print colorful info in CLI.
    You must `kit.require 'colors'` before using it.
    Sometimes use `kit.require 'colors/safe'` will be better.

    - **<u>type</u>**: { _Object_ }

- ## **[daemonize](lib/kit.coffee?source#L300)**

    Daemonize a program. Just a shortcut usage of `kit.spawn`.

    - **<u>param</u>**: `opts` { _Object_ }

        Defaults:
        ```coffee
        {
        	bin: 'node'
        	args: ['app.js']
        	stdout: 'stdout.log' # Can also be a fd
        	stderr: 'stderr.log' # Can also be a fd
        }
        ```

    - **<u>return</u>**: { _Porcess_ }

        The daemonized process.

- ## **[decrypt](lib/kit.coffee?source#L327)**

    A simple decrypt helper. Cross-version of node.

    - **<u>param</u>**: `data` { _Any_ }

    - **<u>param</u>**: `password` { _String | Buffer_ }

    - **<u>param</u>**: `algorithm` { _String_ }

        Default is 'aes128'.

    - **<u>return</u>**: { _Buffer_ }

- ## **[drives](lib/kit.coffee?source#L348)**

    The warp drives.
    You must `kit.require 'drives'` before using it.

    - **<u>type</u>**: { _Object_ }

- ## **[encrypt](lib/kit.coffee?source#L357)**

    A simple encrypt helper. Cross-version of node.

    - **<u>param</u>**: `data` { _Any_ }

    - **<u>param</u>**: `password` { _String | Buffer_ }

    - **<u>param</u>**: `algorithm` { _String_ }

        Default is 'aes128'.

    - **<u>return</u>**: { _Buffer_ }

- ## **[err](lib/kit.coffee?source#L378)**

    A error log shortcut for `kit.log(msg, 'error', opts)`

    - **<u>param</u>**: `msg` { _Any_ }

    - **<u>param</u>**: `opts` { _Object_ }

- ## **[exec](lib/kit.coffee?source#L412)**

    A better `child_process.exec`. Supports multi-line shell script.
    For supporting old node version, it will create 3 temp files,
    the temp files will be removed after the execution.

    - **<u>param</u>**: `cmd` { _String_ }

        Shell commands.

    - **<u>param</u>**: `shell` { _String_ }

        Shell name. Such as `bash`, `zsh`. Optinal.

    - **<u>return</u>**: { _Promise_ }

        Resolves when the process's stdio is drained.
        The resolve value is like:
        ```coffee
        {
        	code: 0
        	signal: null
        	stdout: 'hello world'
        	stderr: ''
        }
        ```

    - **<u>example</u>**:

        ```coffee
        kit.exec("""
        	a='hello world'
         echo $a
        """).then ({code, stdout}) ->
        	kit.log code # output => 0
        	kit.log stdout # output => "hello world"

        # Bash doesn't support "**" recusive match pattern.
        kit.exec """
        	echo **/*.css
        """, 'zsh'
        ```

- ## **[flow](lib/kit.coffee?source#L505)**

    Creates a function that is the composition of the provided functions.
    Besides, it can also accept async function that returns promise.
    See `kit.async`, if you need concurrent support.

    - **<u>param</u>**: `fns` { _Function | Array_ }

        Functions that return
        promise or any value.
        And the array can also contains promises or values other than function.
        If there's only one argument and it's a function, it will treat as an iterator,
        when it returns `kit.flow.end`, the iteration ends.

    - **<u>return</u>**: { _Function_ }

        `(val) -> Promise` A function that will return a promise.

    - **<u>example</u>**:

        ```coffee
        # It helps to decouple sequential pipeline code logic.

        createUrl = (name) ->
        	return "http://test.com/" + name

        curl = (url) ->
        	kit.request(url).then ->
        		kit.log 'get'

        save = (str) ->
        	kit.outputFile('a.txt', str).then ->
        		kit.log 'saved'

        download = kit.flow createUrl, curl, save
        # same as "download = kit.flow [createUrl, curl, save]"

        download 'home'
        ```

    - **<u>example</u>**:

        Walk through first link of each page.
        ```coffee
        list = []
        iter = (url) ->
        	return kit.flow.end if not url

        	kit.request url
        	.then (body) ->
        		list.push body
        		m = body.match /href="(.+?)"/
        		m[0] if m

        walker = kit.flow iter
        walker 'test.com'
        ```

- ## **[formatComment](lib/kit.coffee?source#L563)**

    Format the parsed comments array to a markdown string.

    - **<u>param</u>**: `comments` { _Array_ }

    - **<u>param</u>**: `opts` { _Object_ }

        Defaults:
        ```coffee
        {
        	indent: 0
        	name: ({ name }) ->
        		name = name.replace 'self.', ''
        		"- \#\#\#\# #{name}\n\n"
        	tag: ({ tagName, name, type }) ->
        		tname = if name then " `#{name}`" else ''
        		ttype = if type then " { _#{type}_ }" else ''
        		"- **<u>#{tagName}</u>**:#{tname}#{ttype}"
        }
        ```

    - **<u>return</u>**: { _String_ }

- ## **[fs](lib/kit.coffee?source#L605)**

    See my project [nofs](https://github.com/ysmood/nofs).

    [Offline Documentation](?gotoDoc=nofs/readme.md)

- ## **[fuzzySearch](lib/kit.coffee?source#L634)**

    Fuzzy search a string list by a key word.

    - **<u>param</u>**: `keys` { _String_ }

        The key word.

    - **<u>param</u>**: `list` { _Array_ }

        The list of string to search.

    - **<u>param</u>**: `opts` { _Object_ }

        Defaults:
        ```coffee
        {
        	threshold: (cOffset, keyLen, cIndex) ->
        		Infinity
        	notFound: (cOffset, keyLen, cIndex) ->
        		Infinity
        	span: (cOffset, keyLen, cIndex) ->
        		cOffset
        	found: (cOffset, keyLen, cIndex) ->
        		(Math.exp(cOffset + 1) - 1) * (keyLen - cIndex)
        	tail: (cOffset, keyLen, cIndex, tailLen) ->
        		tailLen
        }
        ```

    - **<u>return</u>**: { _String_ }

        The best matched one. If not found,
        return undefined.

    - **<u>example</u>**:

        ```coffee
        kit.fuzzySearch 'hw', ['test', 'hello world', 'hey world']
        # output => 'hey world'
        ```

- ## **[genModulePaths](lib/kit.coffee?source#L680)**

    Generate a list of module paths from a name and a directory.

    - **<u>param</u>**: `moduleName` { _String_ }

        The module name.

    - **<u>param</u>**: `dir` { _String_ }

        The root path. Default is current working dir.

    - **<u>param</u>**: `modDir` { _String_ }

        Default is 'node_modules'.

    - **<u>return</u>**: { _Array_ }

        Paths

- ## **[indent](lib/kit.coffee?source#L710)**

    Indent a text block.

    - **<u>param</u>**: `text` { _String_ }

    - **<u>param</u>**: `num` { _Int_ }

    - **<u>param</u>**: `char` { _String_ }

    - **<u>param</u>**: `reg` { _RegExp_ }

        Default is `/^/mg`.

    - **<u>return</u>**: { _String_ }

        The indented text block.

    - **<u>example</u>**:

        ```coffee
        # Increase
        kit.indent "one\ntwo", 2
        # => "  one\n  two"

        # Decrease
        kit.indent "--one\n--two", 0, '', /^--/mg
        # => "one\ntwo"
        ```

- ## **[isDevelopment](lib/kit.coffee?source#L720)**

    Nobone use it to check the running mode of the app.
    Overwrite it if you want to control the check logic.
    By default it returns the `rocess.env.NODE_ENV == 'development'`.

    - **<u>return</u>**: { _Boolean_ }

- ## **[isProduction](lib/kit.coffee?source#L729)**

    Nobone use it to check the running mode of the app.
    Overwrite it if you want to control the check logic.
    By default it returns the `rocess.env.NODE_ENV == 'production'`.

    - **<u>return</u>**: { _Boolean_ }

- ## **[jhash](lib/kit.coffee?source#L754)**

    A fast helper to hash string or binary file.
    See my [jhash](https://github.com/ysmood/jhash) project.
    You must `kit.require 'jhash'` before using it.

    [Offline Documentation](?gotoDoc=jhash/readme.md)

    - **<u>example</u>**:

        ```coffee
        kit.require 'jhash'
        kit.jhash.hash 'test' # output => '349o'

        jhash.hash kit.readFileSync('a.jpg')

        # Control the hash char set.
        kit.jhash.setSymbols 'abcdef'
        kit.jhash.hash 'test' # output => 'decfddfe'

        # Control the max length of the result hash value. Unit is bit.
        jhash.setMaskLen 10
        jhash.hash 'test' # output => 'ede'
        ```

- ## **[log](lib/kit.coffee?source#L790)**

    A better log for debugging, it uses the `kit.xinspect` to log.

    Use terminal command like `logReg='pattern' node app.js` to
    filter the log info.

    Use `logTrace='on' node app.js` to force each log end with a
    stack trace.

    - **<u>param</u>**: `msg` { _Any_ }

        Your log message.

    - **<u>param</u>**: `action` { _String_ }

        'log', 'error', 'warn'.

    - **<u>param</u>**: `opts` { _Object_ }

        Default is same with `kit.xinspect`,
        but with some extra options:
        ```coffee
        {
        	isShowTime: true
        	logReg: process.env.logReg and new RegExp process.env.logReg
        	logTrace: process.env.logTrace == 'on'
        }
        ```

    - **<u>example</u>**:

        ```coffee
        kit.log 'test'
        # => '[2015-02-07 08:31:49] test'

        kit.log 'test', { isShowTime: false }
        # => 'test'

        kit.log 'test', { logReg: /a/ }
        # => ''

        kit.log '%s %s %d', ['a', 'b', 10]
        # => '[2015-02-07 08:31:49] a b 10'
        ```

- ## **[logs](lib/kit.coffee?source#L877)**

    Shortcut for logging multiple strings.

    - **<u>param</u>**: `args` { _Any_ }

        ...

    - **<u>example</u>**:

        ```coffee
        kit.log 'test1', 'test2', test3'
        # => [2015-02-07 08:31:49] test1 test2 test3
        ```

- ## **[monitorApp](lib/kit.coffee?source#L943)**

    Monitor an application and automatically restart it when file changed.
    Even when the monitored app exit with error, the monitor will still wait
    for your file change to restart the application. Not only nodejs, but also
    other programs like ruby or python.
    It will print useful infomation when it application unexceptedly.

    - **<u>param</u>**: `opts` { _Object_ }

        Defaults:
        ```coffee
        {
        	bin: 'node'
        	args: ['index.js']
        	watchList: [] # By default, the same with the "args".
        	isNodeDeps: true
        	opts: {} # Same as the opts of 'kit.spawn'.

        	# The option of `kit.parseDependency`
        	parseDependency: {}

        	onStart: ->
        		kit.log "Monitor: " + opts.watchList
        	onRestart: (path) ->
        		kit.log "Reload app, modified: " + path
        	onWatchFiles: (paths) ->
        		kit.log 'Watching:' + paths.join(', ')
        	onNormalExit: ({ code, signal }) ->
        		kit.log 'EXIT' +
        			" code: #{code} signal: #{signal}"
        	onErrorExit: ({ code, signal }) ->
        		kit.err 'EXIT' +
        		" code: #{code} signal: #{signal}\n" +
        		'Process closed. Edit and save
        			the watched file to restart.'
        	sepLine: ->
        		process.stdout.write _.repeat('*', process.stdout.columns)
        }
        ```

    - **<u>return</u>**: { _Promise_ }

        It has a property `process`, which is the monitored
        child process. Properties:
        ```coffee
        {
        	process: Object

        	# Call it to stop monitor.
        	stop: ->

        	# Resolve a list of watch handlers.
        	watchPromise: Promise
        }
        ```

    - **<u>example</u>**:

        ```coffee
        kit.monitorApp {
        	bin: 'coffee'
        	args: ['main.coffee']
        }

        kit.monitorApp {
        	bin: 'ruby'
        	args: ['app.rb', 'lib/**/*.rb']
        	isNodeDeps: false
        }
        ```

- ## **[nodeVersion](lib/kit.coffee?source#L1029)**

    Node version. Such as `v0.10.23` is `0.1023`, `v0.10.1` is `0.1001`.

    - **<u>type</u>**: { _Float_ }

- ## **[defaultArgs](lib/kit.coffee?source#L1058)**

    A helper for arguments type based function override.

    - **<u>param</u>**: `args` { _Array | Object_ }

        The arguments to set.

    - **<u>param</u>**: `defaults` { _Object_ }

        The default argument settings.
        The key value of the setting is the argument name, the value
        is an object, and the key is the type of the argument, the
        value is the default value of the argument.

    - **<u>return</u>**: { _Object_ }

    - **<u>example</u>**:

        ```coffee
        foo = ->
        	args = kit.defaultArgs arguments, {
        		name: { String: 'A' }
        		colors: { Array: [] }
        		family: { String: null }
        		isReal: { Boolean: false }
        		fn: { Function: -> 'callback' }
        	}

        kit.log foo('test', false, ['red'], -> 'nothing')
        # Here the logged value will deeply equal:
        { name: 'test', colors: ['red'], family: null, fn: -> 'nothing' }
        ```

- ## **[parseComment](lib/kit.coffee?source#L1108)**

    A comments parser for javascript and coffee-script.
    Used to generate documentation from source code automatically.
    It will traverse through all the comments of a coffee file.

    - **<u>param</u>**: `code` { _String_ }

        Coffee source code.

    - **<u>param</u>**: `opts` { _Object_ }

        Parser options:
        ```coffee
        {
        	commentReg: RegExp
        	splitReg: RegExp
        	tagNameReg: RegExp
        	typeReg: RegExp
        	nameReg: RegExp
        	nameTags: ['param', 'property']
        	descriptionReg: RegExp
        }
        ```

    - **<u>return</u>**: { _Array_ }

        The parsed comments. Each item is something like:
        ```coffee
        {
        	name: 'parseComment'
        	description: 'A comments parser for coffee-script.'
        	tags: [
        		{
        			tagName: 'param'
        			type: 'string'
        			name: 'code'
        			description: 'The name of the module it belongs to.'
        			index: 256 # The target char index in the file.
        			line: 32 # The line number of the target in the file.
        		}
        	]
        }
        ```

- ## **[parseDependency](lib/kit.coffee?source#L1211)**

    Parse dependency tree by regex. The dependency relationships
    is not a tree, but a graph. To avoid dependency cycle, this
    function only return an linear array of the dependencies,
    from which you won't get the detail relationshops between files.

    - **<u>param</u>**: `entryPaths` { _String | Array_ }

        The file to begin with.

    - **<u>param</u>**: `opts` { _Object_ }

        Defaults:
        ```coffee
        {
        	depReg: /require\s*\(?['"](.+)['"]\)?/gm
        	depRoots: ['']
        	extensions: ['.js', '.coffee', 'index.js', 'index.coffee']

        	# It will handle all the matched paths.
        	# Return false value if you don't want this match.
        	handle: (path) ->
        		path.replace(/^[\s'"]+/, '').replace(/[\s'";]+$/, '')
        }
        ```

    - **<u>return</u>**: { _Promise_ }

        It resolves the dependency path array.

    - **<u>example</u>**:

        ```coffee
        kit.parseDependency 'main.', {
        	depReg: /require\s*\(?['"](.+)['"]\)?/gm
        	handle: (path) ->
        		return path if path.match /^(?:\.|/|[a-z]:)/i
        }
        .then (markdownStr) ->
        	kit.log markdownStr
        ```

- ## **[path](lib/kit.coffee?source#L1274)**

    io.js native module `path`. See `nofs` for more information.

- ## **[Promise](lib/kit.coffee?source#L1282)**

    The promise lib. Now, it uses Bluebird as ES5 polyfill.
    In the future, the Bluebird will be replaced with native
    ES6 Promise. Please don't use any API other than the ES6 spec.

    - **<u>type</u>**: { _Object_ }

- ## **[promisify](lib/kit.coffee?source#L1295)**

    Convert a callback style function to a promise function.

    - **<u>param</u>**: `fn` { _Function_ }

    - **<u>param</u>**: `this` { _Any_ }

        `this` object of the function.

    - **<u>return</u>**: { _Function_ }

        The function will return a promise object.

    - **<u>example</u>**:

        ```coffee
        readFile = kit.promisify fs.readFile, fs
        readFile('a.txt').then kit.log
        ```

- ## **[prop](lib/kit.coffee?source#L1324)**

    Create a getter & setter for an object's property.

    - **<u>param</u>**: `self` { _Object_ }

    - **<u>param</u>**: `prop` { _String_ }

        The property name.

    - **<u>return</u>**: { _Function_ }

        `(v) -> Any`
        ```coffee
        # Two arguments
        data = { path: 'a.txt' }
        txt = kit.prop data, 'txt'
        txt kit.readFile data.path
        .then ->
        	kit.log data
        	kit.log txt()

        # Two arguments another form.
        kit.readFile data.path
        .then txt
        .then ->
        	kit.log data

        # One argument.
        txt = kit.prop 'default value'
        kit.log txt() # => "default value"
        txt 20
        kit.log txt() # => 20
        ```

- ## **[require](lib/kit.coffee?source#L1368)**

    Much faster than the native require of node, but you should
    follow some rules to use it safely.
    Use it to load nokit's internal module.

    - **<u>param</u>**: `moduleName` { _String_ }

        The module path or name.

    - **<u>param</u>**: `dir` { _String_ }

        Current absolute file path. Not optional, expect when
        requiring nokit's internal modules.
        On most times, just pass `__dirname` to it is enough.

    - **<u>param</u>**: `loaded` { _Function_ }

        Run only the first time after the module loaded.

    - **<u>return</u>**: { _Module_ }

        The module that you require.

    - **<u>example</u>**:

        Use it to load nokit's internal module.
        ```coffee
        kit.require 'jhash'
        # Then you can use the module, or it will be null.
        kit.jhash.hash 'test'
        ```
        To load a relative path, or you own module,
        the second parameter 'dir' is required.
        ```coffee
        mod = kit.require './mod', __dirname

        # Or load your own 'jhash', rather than nokit's.
        jhash = kit.require 'jhash', __dirname
        ```

- ## **[requireOptional](lib/kit.coffee?source#L1440)**

    Require an optional package. If not found, it will
    warn the user to npm install it, and exit the process.

    - **<u>param</u>**: `name` { _String_ }

        Package name

    - **<u>param</u>**: `dir` { _String_ }

        Current absolute file path. Not optional.
        On most times, just pass `__dirname` to it is enough.

    - **<u>param</u>**: `semver` { _String_ }

        Specify what version you need,
        such as `^0.3.1` or `>=1.2.3`, ect.

    - **<u>return</u>**: { _Any_ }

        The required package.

- ## **[request](lib/kit.coffee?source#L1557)**

    A handy extended combination of `http.request` and `https.request`.

    - **<u>param</u>**: `opts` { _Object_ }

        The same as the [http.request](http://nodejs.org/api/http.html#httpHttpRequestOptionsCallback),
        but with some extra options:
        ```coffee
        {
        	url: 'It is not optional, String or Url Object.'

        	# Other than return `res` with `res.body`,return `body` directly.
        	body: true

        	# Max times of auto redirect. If 0, no auto redirect.
        	redirect: 0

        	# Timeout of the socket of the http connection.
        	# If timeout happens, the promise will reject.
        	# Zero means no timeout.
        	timeout: 0

        	# The key of headers should be lowercased.
        	headers: {}

        	agent: null

        	# Set "transfer-encoding" header to 'chunked'.
        	setTE: false

        	# Set null to use buffer, optional.
        	# It supports GBK, ShiftJIS etc.
        	# For more info, see https://github.com/ashtuchkin/iconv-lite
        	resEncoding: 'auto'

        	# It's string, object or buffer, optional. When it's an object,
        	# The request will be 'application/x-www-form-urlencoded'.
        	reqData: null

        	# auto end the request.
        	autoEndReq: true

        	# Readable stream.
        	reqPipe: null

        	# Writable stream.
        	resPipe: null

        	# The progress of the request.
        	reqProgress: (complete, total) ->

        	# The progress of the response.
        	resProgress: (complete, total) ->
        }
        ```
        And if set opts as string, it will be treated as the url.

    - **<u>return</u>**: { _Promise_ }

        Contains the http response object,
        it has an extra `body` property.
        You can also get the request object by using `Promise.req`.

    - **<u>example</u>**:

        ```coffee
        p = kit.request 'http://test.com'
        p.req.on 'response', (res) ->
        	kit.log res.headers['content-length']
        p.then (body) ->
        	kit.log body # html or buffer

        kit.request {
        	url: 'https://test.com/a.mp3'
        	body: false
        	resProgress: (complete, total) ->
        		kit.log "Progress: #{complete} / #{total}"
        }
        .then (res) ->
        	kit.log res.body.length
        	kit.log res.headers

        # Send form-data.
        form = new (require 'form-data')
        form.append 'a.jpg', new Buffer(0)
        form.append 'b.txt', 'hello world!'
        kit.request {
        	url: 'a.com'
        	headers: form.getHeaders()
        	setTE: true
        	reqPipe: form
        }
        .then (body) ->
        	kit.log body
        ```

- ## **[semver](lib/kit.coffee?source#L1754)**

    The semantic versioner for npm, known as [semver](https://github.com/npm/node-semver).
    You must `kit.require 'semver'` before using it.

    - **<u>type</u>**: { _Object_ }

- ## **[sleep](lib/kit.coffee?source#L1761)**

    Sleep for awhile.

    - **<u>param</u>**: `time` { _Integer_ }

        Time to sleep, millisecond.

    - **<u>return</u>**: { _Promise_ }

- ## **[spawn](lib/kit.coffee?source#L1795)**

    A safer version of `child_process.spawn` to cross-platform run
    a process. In some conditions, it may be more convenient
    to use the `kit.exec`.
    It will automatically add `node_modules/.bin` to the `PATH`
    environment variable.

    - **<u>param</u>**: `cmd` { _String_ }

        Path or name of an executable program.

    - **<u>param</u>**: `args` { _Array_ }

        CLI arguments.

    - **<u>param</u>**: `opts` { _Object_ }

        Process options.
        Same with the Node.js official documentation.
        Except that it will inherit the parent's stdio.

    - **<u>return</u>**: { _Promise_ }

        The `promise.process` is the spawned child
        process object.
        **Resolves** when the process's stdio is drained and the exit
        code is either `0` or `130`. The resolve value
        is like:
        ```coffee
        {
        	code: 0
        	signal: null
        }
        ```

    - **<u>example</u>**:

        ```coffee
        kit.spawn 'git', ['commit', '-m', '42 is the answer to everything']
        .then ({code}) -> kit.log code
        ```

- ## **[task](lib/kit.coffee?source#L1902)**

    Sequencing and executing tasks and dependencies concurrently.

    - **<u>param</u>**: `name` { _String_ }

        The task name.

    - **<u>param</u>**: `opts` { _Object_ }

        Optional. Defaults:
        ```coffee
        {
        	deps: String | Array
        	description: String
        	logStart: ->
        	logEnd: ->

        	# Whether to run dependency in a row.
        	isSequential: false
        }
        ```

    - **<u>param</u>**: `fn` { _Function_ }

        `(val) -> Promise | Any` The task function.
        If it is a async task, it should return a promise.
        It will get its dependency tasks' resolved values.

    - **<u>property</u>**: `run` { _Function_ }

        Use it to start tasks. Each task will only run once.
        `(names = 'default', opts) ->`. The `names` can be a string or array.
        The default opts:
        ```coffee
        {
        	isSequential: false

        	# Will be passed as the first task's argument.
        	init: undefined

        	# To stop the run currently in process. Set the `$stop`
        	# reference to true. It will reject a "runStopped" error.
        	warp: { $stop: false }
        }
        ```

    - **<u>property</u>**: `list` { _Object_ }

        The defined task functions.

    - **<u>return</u>**: { _Promise_ }

        Resolve with the last task's resolved value.
        When `isSequential == true`, it resolves a value, else it resolves
        an array.

    - **<u>example</u>**:

        ```coffee
        kit.task 'default', { deps: 'build' }, ->
        	kit.log 'run defaults...'

        kit.task 'build', { deps: ['clean'] }, (isFull) ->
        	if isFull
        		'do something'
        	else
        		'do something else'

        kit.task 'clean', (opts) ->
        	if opts.isForce
        		kit.remove 'dist/**', { isForce: true }
        	else
        		kit.remove 'dist/**'

        kit.task.run()
        .then ->
        	kit.log 'All Done!'
        ```

- ## **[url](lib/kit.coffee?source#L1974)**

    The `url` module of [io.js](iojs.org).
    You must `kit.require 'url'` before using it.

- ## **[warp](lib/kit.coffee?source#L2088)**

    Works much like `gulp.src`, but with Promise instead.
    The warp control and error handling is more pleasant.

    - **<u>param</u>**: `from` { _String_ }

        Glob pattern string.

    - **<u>param</u>**: `opts` { _Object_ }

        It extends the options of `nofs.glob`, but
        with some extra proptereis. Defaults:
        ```coffee
        {
        	# The base directory of the pattern.
        	baseDir: String
        }
        ```

    - **<u>return</u>**: { _Object_ }

        The returned warp object has these members:
        ```coffee
        {
        	# The drive can also be a promise that will resolve a drive.
        	load: (drive) -> fileInfo | null

        	run: (path) -> Promise
        }
        ```
        Each piped drive will recieve a
        object that extends `nofs`'s fileInfo object:
        ```coffee
        {
        	# Set the contents and return self.
        	set: (String | Buffer) -> fileInfo

        	# The src file path.
        	path: String

        	# The dest root path.
        	to: String

        	baseDir: String

        	# The destination path.
        	# Alter it if you want to change the output file's location.
        	# You can set it to string, warp will auto-convert it to object.
        	# It's "valueOf" will return "kit.path.join dir, name + ext".
        	dest: { root, dir, base, ext, name }

        	# The file content.
        	contents: String | Buffer

        	isDir: Boolean

        	stats: fs.Stats

        	# Alter it to control the left drives dynamically.
        	drives: [Function]

        	# All the globbed files.
        	list: Array

        	driveList: Array

        	# The opts you passed to "kit.warp", it will be extended.
        	opts: Object
        }
        ```

        The drive can have a `isReader` property, which will make the drive
        override the default file reader.

        The drive can have a `isWriter` property, which will make the drive
        override the default file writer.
        The writer can have a `onEnd` function, which will be called after the
        whole warp ended.

        If a drive overrides another, it can call `fileInfo.super()` to use it again.

    - **<u>example</u>**:

        ```coffee
        # Define a simple workflow.
        kit.warp 'src/**/*.js'
        .load (fileInfo) ->
        	fileInfo.set '/* Lisence Info */' + fileInfo.contents
        .load jslint()
        .load minify()
        .run 'build/minified'

        # Override warp's file reader with a custom one.
        myReader = kit._.extend (fileInfo) ->
        	# Note that we can also use "@path",
        	# its the same with "fileInfo.path" here.
        	kit.readFile @path, 'hex'
        	.then @set
        , {
        	# This will tell warp you want use your own reader.
        	isReader: true
        }

        # Override writer.
        myWriter = kit._.extend (fileInfo) ->
        	return if @dest == 'a.js'

        	# Call the overrided writer.
        	@super()
        , isWriter: true, onEnd: -> @super()
        	kit.log @list

        kit.warp 'src/**/*.js'
        .load myWriter
        .run 'dist'

        # Use nokit's built-in warp drives.
        drives = kit.require 'drives'
        kit.warp src/**/*.coffee'
        .load drives.coffee()
        .run 'dist'
        ```

- ## **[which](lib/kit.coffee?source#L2159)**

    Same as the unix `which` command.
    You must `kit.require 'which'` before using it.

    - **<u>param</u>**: `name` { _String_ }

        The command.

    - **<u>return</u>**: { _Promise_ }

- ## **[whichSync](lib/kit.coffee?source#L2166)**

    Sync version of `which`.
    You must `kit.require 'whichSync'` before using it.

    - **<u>type</u>**: { _Function_ }

- ## **[xinspect](lib/kit.coffee?source#L2177)**

    For debugging. Dump a colorful object.

    - **<u>param</u>**: `obj` { _Object_ }

        Your target object.

    - **<u>param</u>**: `opts` { _Object_ }

        Options. Default:
        ```coffee
        { colors: true, depth: 5 }
        ```

    - **<u>return</u>**: { _String_ }

- ## **[xopen](lib/kit.coffee?source#L2200)**

    Open a thing that your system can recognize.
    Now only support Windows, OSX or system that installed 'xdg-open'.

    - **<u>param</u>**: `cmds` { _String | Array_ }

        The thing you want to open.

    - **<u>param</u>**: `opts` { _Object_ }

        The options of the node native
        `child_process.exec`.

    - **<u>return</u>**: { _Promise_ }

        When the child process exists.

    - **<u>example</u>**:

        ```coffee
        # Open a webpage with the default browser.
        kit.open 'http://ysmood.org'
        ```



# Drives

- ## **[Overview](lib/drives.coffee?source#L10)**

    The built-in plguins for warp. It's more like examples
    to show how to use nokit efficiently.

- ## **[cleanCss](lib/drives.coffee?source#L19)**

    clean-css

    - **<u>param</u>**: `opts` { _Object_ }

    - **<u>return</u>**: { _Function_ }

- ## **[coffee](lib/drives.coffee?source#L33)**

    coffee-script compiler

    - **<u>param</u>**: `opts` { _Object_ }

        Default is `{ bare: true }`.

    - **<u>return</u>**: { _Function_ }

- ## **[coffeelint](lib/drives.coffee?source#L68)**

    coffeelint processor

    - **<u>param</u>**: `opts` { _Object_ }

        It extends the default config
        of coffeelint, properties:
        ```coffee
        {
        	colorize: true
        	reporter: 'default'

        	# The json of the "coffeelint.json".
        	# If it's null, coffeelint will try to find
        	# "coffeelint.json" as its content.
        	config: null | JSON | JsonFilePath
        }
        ```

    - **<u>return</u>**: { _Function_ }

- ## **[comment2md](lib/drives.coffee?source#L123)**

    Parse commment from a js, coffee, or livescript file,
    and output a markdown string.

    - **<u>param</u>**: `path` { _String_ }

    - **<u>param</u>**: `opts` { _Object_ }

        Defaults:
        ```coffee
        {
        	# Output doc path.
        	out: 'readme.md'

        	# jst template path.
        	tpl: 'readme.jst.md'

        	# Init doc info.
        	doc: {}

        	# Header size.
        	h: 3

        	parseComment: -> ...
        	formatComment: -> ...
        }
        ```

    - **<u>return</u>**: { _Function_ }

    - **<u>example</u>**:

        The nofile of nokit shows how to use it.

- ## **[auto](lib/drives.coffee?source#L178)**

    Auto-compiler file by extension. It will search through
    `kit.drives`, and find proper drive to run the task.
    You can extend `kit.drives` to let it support more.
    For example:
    ```coffee
    kit.drives.myCompiler = kit._.extend ->
    	# your compile logic
    , compiler: ['.jsx']
    ```

    - **<u>param</u>**: `action` { _String_ }

        By default, it can be
        'compile' or 'compress' or 'lint'

    - **<u>param</u>**: `opts` { _Object_ }

        ```coffee
        {
        	# If no compiler match.
        	onNotFound: (fileInfo) ->
        }
        ```

    - **<u>return</u>**: { _Function_ }

- ## **[concat](lib/drives.coffee?source#L214)**

    a batch file concat helper

    - **<u>param</u>**: `name` { _String_ }

        The output file path.

    - **<u>param</u>**: `dir` { _String_ }

        Optional. Override the dest of warp's.

    - **<u>return</u>**: { _Function_ }

- ## **[jshint](lib/drives.coffee?source#L240)**

    Lint js via `jshint`.

    - **<u>param</u>**: `opts` { _Object_ }

        Properties:
        ```coffee
        {
        	global: null
        	config: null | JSON | JsonFilePath
        }
        ```

    - **<u>return</u>**: { _Function_ }

- ## **[less](lib/drives.coffee?source#L273)**

    Compile less.

    - **<u>param</u>**: { _Object_ }

    - **<u>return</u>**: { _Function_ }

- ## **[livescript](lib/drives.coffee?source#L300)**

    LiveScript compiler.

    - **<u>param</u>**: `opts` { _Object_ }

        Default is `{ bare: true }`.

    - **<u>return</u>**: { _Function_ }

- ## **[mocha](lib/drives.coffee?source#L329)**

    mocha test

    - **<u>param</u>**: `opts` { _Object_ }

        ```
        {
        	timeout: 5000
        }
        ```

    - **<u>return</u>**: { _Function_ }

- ## **[reader](lib/drives.coffee?source#L359)**

    read file and set `contents`

    - **<u>param</u>**: `opts` { _Object_ }

        Defaults:
        ```coffee
        {
        	isCache: true
        	encoding: 'utf8'
        	cacheDir: '.nokit/warp'
        }
        ```

    - **<u>return</u>**: { _Function_ }

- ## **[stylus](lib/drives.coffee?source#L428)**

    Compile stylus.

    - **<u>param</u>**: `opts` { _Object_ }

        It will use `stylus.set` to
        iterate `opts` and set the key-value, is the value is
        not a function.
        ```coffee
        {
        	config: (styl) ->
        }
        ```

    - **<u>return</u>**: { _Function_ }

    - **<u>example</u>**:

        ```coffee
        kit.drives.stylus {
        	compress: true
        	config: (styl) ->
        		styl.define 'jack', 'a persion'
        }
        ```

- ## **[uglifyjs](lib/drives.coffee?source#L468)**

    uglify-js processor

    - **<u>param</u>**: `opts` { _Object_ }

        Defaults:
        ```coffee
        {
        	output:
        		comments: (node, comment) ->
        			text = comment.value
        			type = comment.type
        			if type == "comment2"
        				return /@preserve|@license|@cc_on/i.test text
        }
        ```

    - **<u>return</u>**: { _Function_ }

- ## **[writer](lib/drives.coffee?source#L490)**

    Output file by `contents` and `dest`.
    If the 'ext' or 'name' is not null,
    the 'base' will be override by the 'ext' and 'name'.

    - **<u>return</u>**: { _Function_ }



# Lisence

MIT